find_package(VTK REQUIRED)

# Create the VTK module
# First we scan the modules in our project to find out the dependency graph
# between them.
vtk_module_scan(
  # `vtk_module_find_modules` would be preferred.
  MODULE_FILES      "${CMAKE_CURRENT_SOURCE_DIR}/vtkLidarSlam/vtk.module"
  # Not building the only module we have is silly.
  REQUEST_MODULES   vtkLidarSlam
  # Store the list of provided modules from this scan.
  PROVIDES_MODULES  modules
  # Disable the tests for our modules.
  ENABLE_TESTS      OFF)

# Build the module we just scanned.
vtk_module_build(MODULES ${modules}
  INSTALL_HEADERS ON
  INSTALL_EXPORT LidarSlam
  LIBRARY_DESTINATION lib
  LICENSE_DESTINATION license)

# Wrap it with Python.
vtk_module_python_default_destination(python_destination)

vtk_module_wrap_python(
  MODULES         ${modules}
  PYTHON_PACKAGE  LidarSlam
  MODULE_DESTINATION "lib/${python_destination}"
  # Static Python modules are almost never wanted.
  BUILD_STATIC    OFF
  INSTALL_HEADERS OFF)

# Create an `__init__.py` for the wrapped module.
file(WRITE "${CMAKE_BINARY_DIR}/lib/${python_destination}/LidarSlam/__init__.py" "")

install(FILES "${CMAKE_BINARY_DIR}/lib/${python_destination}/LidarSlam/__init__.py"
        DESTINATION "${CMAKE_INSTALL_PREFIX}/lib/${python_destination}/LidarSlam/")

include(CMakePackageConfigHelpers)

configure_package_config_file(${CMAKE_CURRENT_SOURCE_DIR}/LidarSlamConfig.cmake.in
  "${CMAKE_CURRENT_BINARY_DIR}/LidarSlamConfig.cmake"
  INSTALL_DESTINATION ${CMAKE_INSTALL_LIBDIR}/cmake/LidarSlam
)

# generate the version file for the config file
write_basic_package_version_file(
  "${CMAKE_CURRENT_BINARY_DIR}/LidarSlamConfigVersion.cmake"
  VERSION "1.0"
  COMPATIBILITY AnyNewerVersion
)
# export(EXPORT LidarSlamModule
#   FILE "${CMAKE_CURRENT_BINARY_DIR}/LidarSlamTargets.cmake"
#   NAMESPACE vtkLidarSlam::
# )

# install(
#     EXPORT      LidarSlam
#     NAMESPACE   vtkLidarSlam::
#     FILE        LidarSlamTargets.cmake
#     DESTINATION ${CMAKE_INSTALL_LIBDIR}/cmake/LidarSlam
#     COMPONENT   "development")

install(FILES
  "${CMAKE_CURRENT_BINARY_DIR}/LidarSlamConfig.cmake"
  "${CMAKE_CURRENT_BINARY_DIR}/LidarSlamConfigVersion.cmake"
  DESTINATION ${CMAKE_INSTALL_LIBDIR}/cmake/LidarSlam
)
